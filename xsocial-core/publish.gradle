apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def bintrayUser
def bintrayApiKey

File propertiesFile = project.rootProject.file('local.properties')
if(propertiesFile.exists()) {
    Properties properties = new Properties()
    properties.load(propertiesFile.newDataInputStream())

    bintrayUser = properties.getProperty('bintray.user')
    bintrayApiKey = properties.getProperty('bintray.key')

} else {
    bintrayUser = System.getenv('BINTRAY_USER')
    bintrayApiKey = System.getenv('BINTRAY_KEY')
}


def versionCode = '1.0.1'
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "newtalentxp"
            email "newtalentxp@126.com"
        }
        developer {
            name "su"
            email "fioman@qq.com"
        }
    }

    scm {
        url "https://github.com/duolacloud/xsocial-android"
    }
}




task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task syncJavadoc(type: Sync, dependsOn: javadoc) {
    from javadoc.destinationDir
    into rootProject.file('docs')
}

javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        String packageListRef = "${android.sdkDirectory}/docs/reference/"
        linksOffline 'http://d.android.com/reference/', packageListRef
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


publishing {
    publications {
        MyPublication(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"
            groupId 'com.duolacloud'
            artifactId 'xsocial-core'
            version versionCode
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A leading identity authentication service platform')
                root.appendNode('name', 'xsocial-core')
                root.appendNode('url', 'https://xauth.lucfish.com/')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey

    publications = ['MyPublication']
    publish = true
    pkg {
        repo = 'xsocial'
        name = 'xsocial-core'
        userOrg = bintrayUser
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/duolacloud/xsocial-android.git'
        version {
            name = versionCode
            mavenCentralSync {
                sync = false //[Default: true] Determines whether to sync the version to Maven Central.
                user = bintrayUser //OSS user token: mandatory
                password = 'it is a secret' //OSS user password: mandatory
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
        websiteUrl = 'https://xauth.lucfish.com/'
        desc = 'A leading identity authentication service platform'
    }
}